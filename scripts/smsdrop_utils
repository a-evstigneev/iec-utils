#!/bin/sh

readvalue_ccu825()
{
	local comaddr timestamp
	
	awk -v comaddr=$1 -v timestamp=$2 '
		BEGIN {
			FS = " "; COMADDR = comaddr; TIMESTAMP = timestamp 
			TYPE["M_SP_TB_1"] = 30
			TYPE["M_ME_TF_1"] = 36
			count = 0
		}
		
		{ 
			objaddr = 0
			value = 0
			
			gsub(/,/, ".", $0)

			if ( $0 ~ "^TC" ) { #TC1-TC16
				objaddr = substr($1, 3)
				value = $2
				type = TYPE["M_SP_TB_1"]
			}
			else if ( $0 ~ "POWER" ) { #TC17
				objaddr = 17
				if ($2 == "OK")
					value = 0
				else if ( $2 == "FAULT" )
					value = 1
				type = TYPE["M_SP_TB_1"]
			}
			else if ( $0 ~ "TEMPERAT" ) { # TI1
				objaddr = 1
				value = substr($2, 1, length($2)-1)
				type = TYPE["M_ME_TF_1"]
			}

			# Empty_data
			if ( objaddr == 0 )
				next
			
			printf("%s %s %s %s %s %s\n", COMADDR, objaddr, type, value, "-", TIMESTAMP)
			count += 1
		}
		
		END {
			if ( count == 0 )
				exit 2
		}	
	' 
	
	return $?
}


readvalue_granit()
{
	local comaddr timestamp

	awk -v comaddr=$1 -v timestamp=$2 '
		BEGIN {
			FS =","; COMADDR = comaddr; TIMESTAMP = timestamp 
			TYPE["M_SP_TB_1"] = 30
			TYPE["M_ME_TF_1"] = 36
			
			OBJADDR["Zona1"] = 1; VALUE["Zona1"] = -1 
			OBJADDR["Zona2"] = 2; VALUE["Zona2"] = -1 
			OBJADDR["Zona3"] = 3; VALUE["Zona3"] = -1 
			OBJADDR["Zona4"] = 4; VALUE["Zona4"] = -1 
			OBJADDR["Akk"] 	 = 5; VALUE["Akk"]   = -1 
			OBJADDR["Set"]   = 6; VALUE["Set"]   = -1 
			OBJADDR["t="]    = 1; VALUE["t="]
		}
		
		{ 
			gsub(/\047/, "", $0)
			
			if ( ( $0 ~ /Vzayty na ohrany/ ) || ( $0 ~ /Snayty s ohrany/ ) )
				exit 2
			if ( $0 !~ /.*Zona.:.*Akk.*Set.*t=.*gradus C/ )
				exit 2
			
			gsub(/ gradus C/, "", $0)
			
			for (i = 1; i <= NF; ++i) {
				for (field in OBJADDR) { 
					if ( $i ~ field ) {
						if ( $i ~ /norma/ )
							VALUE[field] = 0
						else if ( ( $i ~ /Neispravnost/ ) || ( $i ~ /Pogar/ ) || ( $i ~ /Trevoga/ ) )
							VALUE[field] = 1
						else if ( $i ~ /Net/ )
							VALUE[field] = 1
						else if ( $i ~ /t=/ ) 
							VALUE[field] = substr($i, index($i, "=") + 1)
						
					}
				}
			}
			
			for (field in OBJADDR) 
				if ( ( field == "t=" ) && ( VALUE[field] != "" ) )
					printf("%s %s %s %s %s %s\n", COMADDR, OBJADDR[field], TYPE["M_ME_TF_1"], VALUE[field], "-", TIMESTAMP)
				else if ( VALUE[field] != -1 ) 
					printf("%s %s %s %s %s %s\n", COMADDR, OBJADDR[field], TYPE["M_SP_TB_1"], VALUE[field], "-", TIMESTAMP)
				
		}
	' 

	return $?
}

readvalue_verset()
{
	local comaddr timestamp

	awk -v comaddr=$1 -v timestamp=$2 '
		BEGIN {
			FS = " "; COMADDR = comaddr; TIMESTAMP = timestamp 
			TYPE["M_SP_TB_1"] = 30
		}
		
		{ 
			value = 0
			objaddr = 0
			
			if ( $0 ~ "Взята на охрану зона" )
				objaddr = $8
			else if ( $0 ~ "Восстановление технолог. зоны" )
				objaddr = $7  
			else if ( $0 ~ "Нарушение технологической зоны" || $0 ~ "Неисправность в зоне" ) {
				objaddr = $7; value = 1
			}
			
			# TC 10
			if ( $0 ~ "Прибор закрыт" ) 
				objaddr = 10
			else if ( $0 ~ "Прибор открыт" ) {
				objaddr = 10; value = 1
			}
			
			# TC 11
			if ( $0 ~ "Сеть двести двадцать вольт включена" )
				objaddr = 11
			else if ( $0 ~ "Сеть двести двадцать вольт выключена" ) {
				objaddr = 11; value = 1  
			}

			# TC 12
			if ( $0 ~ "Аккумулятор в норме" )
				objaddr = 12
			else if ( $0 ~ "Аккумулятор отсутствует" || $0 ~ "Аккумулятор разряжен" ) {
				objaddr = 12; value = 1  
			}
			
			# Invalid data
			if ( objaddr == 0 ) 
				exit 2
			
			printf("%s %s %s %s %s %s\n", COMADDR, objaddr, TYPE["M_SP_TB_1"], value, "-", TIMESTAMP)
		}
	' 

	return $?
}
